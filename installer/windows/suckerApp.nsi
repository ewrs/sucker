; Script generated by the HM NIS Edit Script Wizard.

Unicode True
CRCCheck On

!include "x64.nsh"

!include "AdvReplaceInFile.nsh"

!include "StrFunc.nsh"
${StrRep}

!searchparse /file ../../version.txt `` prodVersion

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "suckerApp"
!define PRODUCT_VERSION "v${prodVersion}"
!define PRODUCT_PUBLISHER "ewrs"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

!define MOZ_NATIVE_MESSAGING "SOFTWARE\Mozilla\NativeMessagingHosts"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "sucker.ico"
!define MUI_UNICON "sucker.ico"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Installer pages
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH

; Language files
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "German"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "..\..\dist\${PRODUCT_NAME}-${PRODUCT_VERSION}-setup.exe"
InstallDir "$PROGRAMFILES64\${PRODUCT_NAME}"
ShowInstDetails nevershow
ShowUnInstDetails nevershow

!macro INST_UNINST un
Function ${un}GrantRegKeyAccess
  Push $0 ; save
  Push "*" ; marker
  AccessControl::GrantOnRegKey HKLM "${MOZ_NATIVE_MESSAGING}" "(BU)" "FullAccess"
  Pop $0 ; marker or error msg
  StrCmp $0 "*" +2
  Pop $0 ; marker
  Pop $0 ; restore
FunctionEnd
!macroend

; Insert function as an installer and uninstaller function.
!insertmacro INST_UNINST ""
!insertmacro INST_UNINST "un."

Function .onInit
  !insertmacro MUI_LANGDLL_DISPLAY
  SetRegView 64
FunctionEnd

Section "Hauptgruppe" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite on
  File "..\..\xpi\install\${PRODUCT_NAME}.bat"
  File "..\..\dist\${PRODUCT_NAME}.jar"
  File "..\..\xpi\install\${PRODUCT_NAME}.json"
  File "bin\*.exe"
  File /r "..\..\build\jre"

  Var /GLOBAL jsonInstDir
  ${StrRep} "$jsonInstDir" "$INSTDIR" "\" "\\" #escape backslashes for json
  Push "@@InstDir@@" #text to be replaced
  Push "$jsonInstDir" #replace with
  Push all #replace all occurrences
  Push all #replace all occurrences
  Push "$INSTDIR\${PRODUCT_NAME}.json" #file to replace in
  Call AdvReplaceInFile
  
  Push "call java" #text to be replaced
  Push "call jre\bin\java" #replace with
  Push all #replace all occurrences
  Push all #replace all occurrences
  Push "$INSTDIR\${PRODUCT_NAME}.bat" #file to replace in
  Call AdvReplaceInFile

  Call GrantRegKeyAccess
  WriteRegStr HKLM "${MOZ_NATIVE_MESSAGING}\${PRODUCT_NAME}" "" "$INSTDIR\${PRODUCT_NAME}.json"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\${PRODUCT_NAME}-uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\${PRODUCT_NAME}-uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
SectionEnd

Function un.onInit
  !insertmacro MUI_UNGETLANGUAGE
  SetRegView 64
FunctionEnd

Section Uninstall
  RMDir /r "$INSTDIR"

  Call un.GrantRegKeyAccess
  DeleteRegKey HKLM "${MOZ_NATIVE_MESSAGING}\${PRODUCT_NAME}"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
SectionEnd
