; Script generated by the HM NIS Edit Script Wizard.

Unicode True
CRCCheck On

!include "x64.nsh"

!include "AdvReplaceInFile.nsh"

!include "StrFunc.nsh"
${StrRep}

!searchparse /file ../../version.txt `` prodVersion

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "suckerApp"
!define PRODUCT_VERSION "v${prodVersion}"
!define PRODUCT_PUBLISHER "ewrs"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "sucker.ico"
!define MUI_UNICON "sucker.ico"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "German"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "..\..\dist\${PRODUCT_NAME}.${PRODUCT_VERSION}.setup.exe"
InstallDir "$PROGRAMFILES64\${PRODUCT_NAME}"
ShowInstDetails nevershow
ShowUnInstDetails nevershow

Function .onInit
  !insertmacro MUI_LANGDLL_DISPLAY
  SetRegView 64
FunctionEnd

Section "Hauptgruppe" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite on
  File "..\..\xpi\install\${PRODUCT_NAME}.bat"
  File "..\..\dist\${PRODUCT_NAME}.jar"
  File "..\..\xpi\install\${PRODUCT_NAME}.json"
  File "bin\*.*"
  File /r "..\..\build\jre"

  Var /GLOBAL jsonInstDir
  ${StrRep} "$jsonInstDir" "$INSTDIR" "\" "\\" #escape backslashes for json
  Push "@@InstDir@@" #text to be replaced
  Push "$jsonInstDir" #replace with
  Push all #replace all occurrences
  Push all #replace all occurrences
  Push "$INSTDIR\${PRODUCT_NAME}.json" #file to replace in
  Call AdvReplaceInFile
  
  Push "call java" #text to be replaced
  Push "call jre\bin\java" #replace with
  Push all #replace all occurrences
  Push all #replace all occurrences
  Push "$INSTDIR\${PRODUCT_NAME}.bat" #file to replace in
  Call AdvReplaceInFile

  AccessControl::GrantOnRegKey HKLM "SOFTWARE\Mozilla\NativeMessagingHosts" "(BU)" "FullAccess"
  Pop $0
  WriteRegStr HKLM "SOFTWARE\Mozilla\NativeMessagingHosts\${PRODUCT_NAME}" "" "$INSTDIR\${PRODUCT_NAME}.json"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
SectionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "${PRODUCT_NAME} wurde erfolgreich deinstalliert."
FunctionEnd

Function un.onInit
!insertmacro MUI_UNGETLANGUAGE
  SetRegView 64
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Möchten Sie ${PRODUCT_NAME} und alle seine Komponenten deinstallieren?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  RMDir /r "$INSTDIR"

  AccessControl::GrantOnRegKey HKLM "SOFTWARE\Mozilla\NativeMessagingHosts" "(BU)" "FullAccess"
  Pop $0
  DeleteRegKey HKLM "SOFTWARE\Mozilla\NativeMessagingHosts\${PRODUCT_NAME}"
  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  SetAutoClose true
SectionEnd
